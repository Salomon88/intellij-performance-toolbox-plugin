buildscript {
    ext {
        junitVersion = '5.6.2'
        // as Idea bundles with org.apache.xmlbeans:xmlbeans:2.6.0, but poi-ooxml 4.1.2 requires 3.1.0, so downgrading
        poiVersion = '3.17'
    }
}

plugins {
    id 'jacoco'
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.21'
    id 'org.jetbrains.kotlin.jvm' version '1.4.0'
    id 'org.sonarqube' version '3.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

wrapper {
    gradleVersion = '6.7.1'
}

group 'org.performancetoolbox'
version '1.0.2'

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'com.github.chewiebug', name: 'gcviewer', version: '1.36'
    implementation group: 'org.apache.poi', name: 'poi', version: "$poiVersion"
    implementation group: 'org.apache.poi', name: 'poi-ooxml', version: "$poiVersion"
    implementation group: 'org.reflections', name: 'reflections', version: '0.9.12'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: "$junitVersion"
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: "$junitVersion"
}

intellij {
    version '2020.1.1'
}

patchPluginXml {
    changeNotes """
        <b>Changes in version 1.0.2:</b>
        <ul>
            <li>Support for IntelliJ IDEA 2021.1</li>
        </ul>
        <b>Changes in version 1.0.1:</b>
        <ul>
            <li>Support for IntelliJ IDEA 2020.3</li>
            <li>Improvements: <a href="https://github.com/Salomon88/intellij-performance-toolbox-plugin/issues/31">#31</a></li>
            <li>Improvements in Histogram Analyzer: <a href="https://github.com/Salomon88/intellij-performance-toolbox-plugin/issues/34">#34</a></li>
        </ul>
        <b>Changes in version 1.0.0:</b>
        <ul>
            <li>Support for GCViewer 1.36</li>
            <li>Preview of histogram series analyzer</li>
        </ul>"""
    untilBuild = '211.*'
}

jacoco {
    toolVersion = "0.8.5"
    reportsDir = file("${project.projectDir}/build/reports")
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.enabled true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "Salomon88_intellij-performance-toolbox-plugin"
        property "sonar.organization", "salomon88"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.exclusions", "**/actions/**,**/com/tagtraum/**,**/plugin/common/D*.java,**/plugin/common/V*.java"
    }
}

test {
    useJUnitPlatform()
}

project.tasks["sonarqube"].dependsOn "jacocoTestReport"